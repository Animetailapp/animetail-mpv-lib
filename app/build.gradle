apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86":3, "x86_64":4]

android {
    namespace 'is.xyz.mpv'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 31
        versionName "1.9.n"
        vectorDrawables.useSupportLibrary = true
    }

    flavorDimensions "default"
    productFlavors {
        "default" {
            isDefault true
        }
        api29 {
            targetSdkVersion 29
            versionNameSuffix "-oldapi"
        }
    }

    buildFeatures {
        viewBinding = true
    }

    splits {
        abi {
            enable true
            reset()
            // include only the ABIs that were actually built
            project.ext.abiCodes.each { abi, _ ->
                if (new File(project.projectDir, "src/main/jniLibs/${abi}").exists())
                    include(abi)
            }
            universalApk true // build an APK with all ABIs too
        }
    }

    // https://youtrack.jetbrains.com/issue/KT-55947
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11.toString()
    }

    packagingOptions {
        exclude '**/libavcodec.so'
        exclude '**/libavdevice.so'
        exclude '**/libavfilter.so'
        exclude '**/libavformat.so'
        exclude '**/libavutil.so'
        exclude '**/libswresample.so'
        exclude '**/libswscale.so'
        exclude '**/libc++_shared.so'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.media:media:1.6.0'
}

def getVersionName = { ->
    return "1.9.n"
}

def getGroupId = { ->
    return "com.github.jmir1"
}


task sourceJar(type: Jar) {
    archiveClassifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            version getVersionName()
            // Place the path of your artifact here
            artifact("$buildDir/outputs/aar/aniyomi-mpv-lib-release.aar")
            artifact(tasks["sourceJar"])
        }
    }
}
